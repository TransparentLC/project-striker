# 四面关底BOSS
# 这个BOSS战会比较长……一共十四个形态
# 66737 - 70165 3428 摩多罗隐岐奈式非符，顺时针/逆时针交替转圈x1x5
# 61518 - 66737 5219 “神之粥”？
# 57795 - 61518 3723 摩多罗隐岐奈式非符，顺时针/逆时针同时转圈x1x4
# 51997 - 57795 5798 “误导”？
# 48017 - 51997 3980 摩多罗隐岐奈式非符，顺时针/逆时针同时转圈x2x2
# 41784 - 48017 6233 “井下的毁灭者”？击破后送hyper
# 37595 - 41784 4189 摩多罗隐岐奈式非符，逆时针连续转圈x3x3
# 30980 - 37595 6615 “旧秘境史 -古代史-”？
# 26617 - 30980 4363 摩多罗隐岐奈式非符，顺时针/逆时针同时转圈x3x2
# 26587 - 26617   30 “你给我变大吧”？30s时符
# 21851 - 26587 4736 摩多罗隐岐奈式非符，顺时针/逆时针交替转圈带小偏转x3x4
# 14998 - 21851 6853 “波与粒的境界”？
#  9999 - 14998 4999 摩多罗隐岐奈式非符，顺时针/逆时针交替转圈x9x2
#     0 -  9999 9999 “无政府弹幕地狱”？

SET_TEXTURE BOSS_D

STORE 0 %ENEMY_X%
SUB 0 192
RANDOM_FLOAT 1 .3 .7
MUL 0 %VAR_1%
ADD 0 192
RANDOM_INT 1 180 240
MOVE %VAR_0% %VAR_1% 90 EASE_OUT_QUAD
WAIT 300
MOVE 192 108 180 EASE_INOUT_CUBIC
WAIT 240

SET_EXPLOSION LARGE
SET_EXPLODE_SFX EXPLODE_ENEMY_D
SET_HITBOX 0 -16 4
SET_HITBOX 0 -6 8
SET_HITBOX 0 5 5
SET_HITBOX 0 13 5
SET_HITBOX -7 14 4
SET_HITBOX -9 -6 6
SET_HITBOX -17 -5 4
SET_HITBOX -22 -5 4
SET_HITBOX -28 -5 3
SET_HITBOX 7 14 4
SET_HITBOX 9 -6 6
SET_HITBOX 17 -5 4
SET_HITBOX 22 -5 4
SET_HITBOX 28 -5 3
SET_DEBRIS DEBRIS_A 0 -16 .5 5 3 7 3
SET_DEBRIS DEBRIS_A 0 -6 .5 5 3 7 2
SET_DEBRIS DEBRIS_B 0 5 .5 5 3 7 2
SET_DEBRIS DEBRIS_A 0 13 .5 5 3 7 2
SET_DEBRIS DEBRIS_B -7 14 .5 5 3 7 3
SET_DEBRIS DEBRIS_B -9 -6 .5 5 3 7 3
SET_DEBRIS DEBRIS_A -17 -5 .5 5 3 7 2
SET_DEBRIS DEBRIS_A -22 -5 .5 5 3 7 2
SET_DEBRIS DEBRIS_B -28 -5 .5 5 3 7 3
SET_DEBRIS DEBRIS_A 7 14 .5 5 3 7 2
SET_DEBRIS DEBRIS_B 9 -6 .5 5 3 7 3
SET_DEBRIS DEBRIS_A 17 -5 .5 5 3 7 2
SET_DEBRIS DEBRIS_B 22 -5 .5 5 3 7 2
SET_DEBRIS DEBRIS_B 28 -5 .5 5 3 7 3

SET_BOSS

SET_BOSS_REMAIN 13
SET_BOSS_HPRANGE 66737 70165
SET_HITPOINT 70165
REGISTER_BOSSBREAK @YWRlhJsC
SET_PHASE_NAME 15
SET_PHASE_BONUS 42 159
SET_INVINCIBLE 30
WAIT 30

STORE 6 6
STORE 5 %PLAYER_X% @ 2lfdQhTC
SUB 5 10
MOVE_RANDOM %VAR_5% 60 20 80 135 EASE_OUT_CUBIC
CALC_DIRECTION 7 %ENEMY_X% %ENEMY_Y% %PLAYER_X% %PLAYER_Y%
STORE 0 10
MUL 0 %VAR_6%
SUB 7 %VAR_0%
STORE 0 0
    STORE 1 %VAR_0% @ yfo74nzA
    MOD 1 5
    JUMP_NE %VAR_1% 0 @MpaufKb3
    SFX ENEMY_SHOOT_D
    SHOOT TYPE_C 0 0 2.5 %VAR_7% @ MpaufKb3
    SHOOT TYPE_C 0 0 3 %VAR_7%
    SHOOT TYPE_C 0 0 3.5 %VAR_7%
    SHOOT TYPE_C 0 0 4 %VAR_7%
    SHOOT TYPE_C 0 0 4.5 %VAR_7%
    ADD 7 %VAR_6%
    WAIT 2
INC 0
JUMP_L %VAR_0% 80 @yfo74nzA
MUL 6 -1
JUMP @2lfdQhTC

SET_BOSS_REMAIN 12 @ YWRlhJsC
SET_BOSS_HPRANGE 61518 66737
SET_HITPOINT 66737
REGISTER_BOSSBREAK @dq735gzN
DROP_POINTITEM 15
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 16
SET_PHASE_BONUS 43 163
WAIT 30

SFX ENEMY_SHOOT_C @ 9rpI7wa4
SHOOT_AIMING_MULTIWAY TYPE_A -30 -5 1.75 0 360 35
SHOOT_AIMING_MULTIWAY TYPE_A 30 -5 1.75 0 360 35
WAIT 25
SFX ENEMY_SHOOT_C
SHOOT_AIMING_MULTIWAY TYPE_B 0 -19 2 0 360 50
WAIT 25
JUMP @9rpI7wa4

SET_BOSS_REMAIN 11 @ dq735gzN
SET_BOSS_HPRANGE 57795 61518
SET_HITPOINT 61518
REGISTER_BOSSBREAK @RZFjyoFA
DROP_POINTITEM 18
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 17
SET_PHASE_BONUS 44 171
WAIT 30

STORE 4 12
STORE 5 %PLAYER_X% @ 1OEbJdHG
SUB 5 10
MOVE_RANDOM %VAR_5% 60 20 80 135 EASE_OUT_CUBIC
CALC_DIRECTION 6 %ENEMY_X% %ENEMY_Y% %PLAYER_X% %PLAYER_Y%
RANDOM_FLOAT 0 -2.5 2.5
ADD 6 %VAR_0%
STORE 7 %VAR_6%
RANDOM_FLOAT 0 -2.5 2.5
ADD 7 %VAR_0%
ADD 6 90
STORE 0 3
MUL 0 %VAR_4%
SUB 6 %VAR_0%
SUB 7 90
STORE 0 3
MUL 0 %VAR_4%
ADD 7 %VAR_0%
STORE 0 0
    STORE 1 %VAR_0% @ DpIuI9PN
    MOD 1 6
    JUMP_NE %VAR_1% 0 @ol2Y968f
    SFX ENEMY_SHOOT_D
    SHOOT TYPE_C -23 -5 2 %VAR_6% @ ol2Y968f
    SHOOT TYPE_C -23 -5 2.5 %VAR_6%
    SHOOT TYPE_C -23 -5 3 %VAR_6%
    SHOOT TYPE_C -23 -5 3.5 %VAR_6%
    SHOOT TYPE_C 23 -5 2 %VAR_7%
    SHOOT TYPE_C 23 -5 2.5 %VAR_7%
    SHOOT TYPE_C 23 -5 3 %VAR_7%
    SHOOT TYPE_C 23 -5 3.5 %VAR_7%
    ADD 6 %VAR_4%
    SUB 7 %VAR_4%
    WAIT 2
INC 0
JUMP_L %VAR_0% 36 @DpIuI9PN
MUL 4 -1
JUMP @1OEbJdHG

SET_BOSS_REMAIN 10 @ RZFjyoFA
SET_BOSS_HPRANGE 51997 57795
SET_HITPOINT 57795
REGISTER_BOSSBREAK @8spzLE1K
DROP_POINTITEM 22
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR

MOVE 192 64 90 EASE_INOUT_CUBIC
SET_INVINCIBLE 120
WAIT 90
SET_PHASE_NAME 18
SET_PHASE_BONUS 45 176
WAIT 30

STORE 6 150
STORE 7 -300

WAIT 30 @ ws3Y6KMQ
MOVE_RELATIVE %VAR_6% 0 90 LINEAR
STORE 0 0
    SFX ENEMY_SHOOT_B @ bB9DodKA
    RANDOM_FLOAT 1 0 360
    SHOOT_MULTIWAY TYPE_B 0 0 1.25 %VAR_1% 360 15
    WAIT 10
INC 0
JUMP_L %VAR_0% 9 @bB9DodKA
MOVE_RELATIVE %VAR_7% 0 10 EASE_OUT_CUBIC
WAIT 10
STORE 0 0
    SFX ENEMY_SHOOT_C @ IqZIrcAt
    SHOOT_AIMING_MULTIWAY TYPE_E 0 0 1.75 0 120 11
    SHOOT_AIMING_MULTIWAY TYPE_E 0 0 2.5 0 120 11
    WAIT 30
INC 0
JUMP_L %VAR_0% 5 @IqZIrcAt
MOVE_RELATIVE %VAR_6% 0 120 LINEAR
WAIT 120
MUL 6 -1
MUL 7 -1
JUMP @ws3Y6KMQ

SET_BOSS_REMAIN 9 @ 8spzLE1K
SET_BOSS_HPRANGE 48017 51997
SET_HITPOINT 51997
REGISTER_BOSSBREAK @qGTByin5
DROP_POINTITEM 25
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 19
SET_PHASE_BONUS 46 182
WAIT 30

STORE 4 12
STORE 5 %PLAYER_X% @ bxCH5x8i
SUB 5 10
MOVE_RANDOM %VAR_5% 60 20 80 135 EASE_OUT_CUBIC
CALC_DIRECTION 6 %ENEMY_X% %ENEMY_Y% %PLAYER_X% %PLAYER_Y%
RANDOM_FLOAT 0 -10 10
ADD 6 %VAR_0%
STORE 7 %VAR_6%
RANDOM_FLOAT 0 -10 10
ADD 7 %VAR_0%
STORE 0 3
MUL 0 %VAR_4%
SUB 6 %VAR_0%
STORE 0 3
MUL 0 %VAR_4%
ADD 7 %VAR_0%
STORE 0 0
    STORE 1 %VAR_0% @ 1tSt2Ehz
    MOD 1 6
    JUMP_NE %VAR_1% 0 @HeVk36rH
    SFX ENEMY_SHOOT_D
    SHOOT_MULTIWAY TYPE_C -8 -5 3 %VAR_6% 180 2 @ HeVk36rH
    SHOOT_MULTIWAY TYPE_C -8 -5 3.5 %VAR_6% 180 2
    SHOOT_MULTIWAY TYPE_C 8 -5 3 %VAR_7% 180 2
    SHOOT_MULTIWAY TYPE_C 8 -5 3.5 %VAR_7% 180 2
    ADD 6 %VAR_4%
    SUB 7 %VAR_4%
    WAIT 2
INC 0
JUMP_L %VAR_0% 36 @1tSt2Ehz
MUL 4 -1
JUMP @bxCH5x8i

SET_BOSS_REMAIN 8 @ qGTByin5
SET_BOSS_HPRANGE 41784 48017
SET_HITPOINT 48017
REGISTER_BOSSBREAK @vGkqjWCd
DROP_POINTITEM 28
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 20
SET_PHASE_BONUS 47 184
WAIT 30

STORE 7 140
STORE 0 0
    STORE 1 %VAR_0% @ qAqlEHiD
    MOD 1 24
    JUMP_NE %VAR_1% 0 @WZsdqmLT
    MUL 7 -1
    RANDOM_INT 1 194 254
    ADD 1 %VAR_7%
    MOVE %ENEMY_X% %VAR_1% 60 EASE_OUT_CUBIC
    JUMP @rIGVTXDB

    STORE 1 %VAR_0% @ WZsdqmLT
    MOD 1 6
    JUMP_NE %VAR_1% 0 @rIGVTXDB
    RANDOM_INT 1 194 254
    ADD 1 %VAR_7%
    MOVE %PLAYER_X% %VAR_1% 60 EASE_OUT_CUBIC
    SFX ENEMY_SHOOT_C
    SHOOT_AIMING TYPE_E 0 0 1.5 0
    SHOOT_AIMING TYPE_E 0 0 2 0
    SHOOT_AIMING TYPE_E 0 0 2.5 0
    SHOOT_AIMING TYPE_E 0 0 3 0
    SHOOT_AIMING TYPE_E 0 0 3.5 0
    SHOOT_AIMING TYPE_E 0 0 4 0
    SHOOT_AIMING TYPE_E 0 0 4.5 0
    SFX ENEMY_SHOOT_A @ rIGVTXDB
    RANDOM_INT 2 -40 40
    RANDOM_INT 3 -8 0
    RANDOM_FLOAT 4 -75 75
    RANDOM_FLOAT 5 .5 2
    SHOOT_AIMING TYPE_B %VAR_2% %VAR_3% %VAR_5% %VAR_4%

    INC 0
    WAIT 10
JUMP @qAqlEHiD

SET_BOSS_REMAIN 7 @ vGkqjWCd
SET_BOSS_HPRANGE 37595 41784
SET_HITPOINT 41784
REGISTER_BOSSBREAK @HT1VwUsR
DROP_POINTITEM 30
EXTEND_HYPER
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 21
SET_PHASE_BONUS 48 191
WAIT 30

STORE 0 0
RANDOM_INT 1 0 360
    STORE 2 %VAR_0% @ wzWt3hpd
    MOD 2 60
    JUMP_NE %VAR_2% 0 @StMp575l
    MOVE_RANDOM 102 60 180 80 90 EASE_OUT_CUBIC
    STORE 2 %VAR_0% @ StMp575l
    MOD 2 5
    JUMP_NE %VAR_2% 0 @8oeQXe8i
    SFX ENEMY_SHOOT_D
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 2.75 %VAR_1% 240 3 @ 8oeQXe8i
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 3 %VAR_1% 240 3
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 3.25 %VAR_1% 240 3
    WAIT 2
    SUB 1 7
    INC 0
JUMP @wzWt3hpd

SET_BOSS_REMAIN 6 @ HT1VwUsR
SET_BOSS_HPRANGE 30980 37595
SET_HITPOINT 37595
REGISTER_BOSSBREAK @U5XxiHPl
DROP_POINTITEM 32
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 100 90 EASE_INOUT_CUBIC
SET_INVINCIBLE 120
WAIT 90
SET_PHASE_NAME 22
SET_PHASE_BONUS 49 193
WAIT 30

STORE 5 0
STORE 6 45 @ AJgejeS1
STORE 7 -45
MOD 5 2

STORE 0 0
    SFX ENEMY_SHOOT_A @ glIONLl9
    JUMP_NE %VAR_5% 0 @EvMOOA6T
    SHOOT_MULTIWAY TYPE_B -40 80 1.5 %VAR_6% 270 4
    SHOOT_MULTIWAY TYPE_B 40 80 1.5 %VAR_7% 270 4
    SHOOT_MULTIWAY TYPE_B -120 0 1.5 %VAR_7% 270 4
    SHOOT_MULTIWAY TYPE_B 120 0 1.5 %VAR_6% 270 4
    JUMP @Dxw8jJtU
    SHOOT_MULTIWAY TYPE_B 40 20 1.5 %VAR_6% 270 4 @ EvMOOA6T
    SHOOT_MULTIWAY TYPE_B -40 20 1.5 %VAR_7% 270 4
    SHOOT_MULTIWAY TYPE_B 140 70 1.5 %VAR_7% 270 4
    SHOOT_MULTIWAY TYPE_B -140 70 1.5 %VAR_6% 270 4
    ADD 6 10 @ Dxw8jJtU
    SUB 7 10
    WAIT 18
INC 0
JUMP_L %VAR_0% 10 @glIONLl9
STORE 0 0
    SFX ENEMY_SHOOT_C @ JQGKBYoo
    RANDOM_FLOAT 1 -3 3
    SHOOT_AIMING_MULTIWAY TYPE_E 0 0 3.5 %VAR_1% 120 9
    WAIT 5
INC 0
JUMP_L %VAR_0% 15 @JQGKBYoo
INC 5
JUMP @AJgejeS1

SET_BOSS_REMAIN 5 @ U5XxiHPl
SET_BOSS_HPRANGE 26617 30980
SET_HITPOINT 30980
REGISTER_BOSSBREAK @9nVrR3Bc
DROP_POINTITEM 33
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 23
SET_PHASE_BONUS 50 202
WAIT 30

STORE 0 0
RANDOM_INT 1 0 360
STORE 2 %VAR_1%
    STORE 3 %VAR_0% @ KY5kmNCr
    MOD 3 60
    JUMP_NE %VAR_3% 0 @8iSPJ9pa
    MOVE_RANDOM 142 60 100 80 90 EASE_OUT_CUBIC
    STORE 3 %VAR_0% @ 8iSPJ9pa
    MOD 3 5
    JUMP_NE %VAR_3% 0 @Sn2oJ5L3
    SFX ENEMY_SHOOT_D
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 2.75 %VAR_1% 240 3 @ Sn2oJ5L3
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 3 %VAR_1% 240 3
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 2.75 %VAR_2% 240 3
    SHOOT_AIMING_MULTIWAY TYPE_C 0 0 3 %VAR_2% 240 3
    WAIT 2
    SUB 1 7
    ADD 2 7
    INC 0
JUMP @KY5kmNCr

SET_BOSS_REMAIN 4 @ 9nVrR3Bc
SET_BOSS_HPRANGE 26587 26617
SET_HITPOINT 26617
REGISTER_BOSSBREAK @xnoniAT8
DROP_POINTITEM 35
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 420 240 EASE_INOUT_CUBIC
SET_INVINCIBLE 270
WAIT 240
SET_PHASE_NAME 24
SET_PHASE_BONUS 51 0
WAIT 30

SET_INVINCIBLE 1800

STORE 6 180
STORE 7 5
STORE 0 0
    STORE 1 %VAR_0% @ RUJauQ3M
    MOD 1 3
    JUMP_NE %VAR_1% 0 @Cwo71H0n
    STORE 1 %HITPOINT%
    DEC 1
    SET_HITPOINT %VAR_1%

    SFX ENEMY_SHOOT_D @ Cwo71H0n
    SHOOT_MULTIWAY TYPE_B_2X 0 0 1.5 %VAR_6% 310 18
    ADD 6 %VAR_7%
    JUMP_G %VAR_6% 200 @LmbkzKub
    JUMP_L %VAR_6% 160 @LmbkzKub
    JUMP @Jy0Yhgc3
    MUL 7 -1 @ LmbkzKub

    STORE 1 %VAR_0% @ Jy0Yhgc3
    MOD 1 10
    JUMP_L %VAR_1% 5 @z7iGtqEv
    SFX ENEMY_SHOOT_B
    SHOOT_AIMING_MULTIWAY TYPE_E_2X -30 -5 3 0 50 3
    SHOOT_AIMING_MULTIWAY TYPE_E_2X 30 -5 3 0 50 3

    WAIT 20 @ z7iGtqEv
INC 0
JUMP @RUJauQ3M

SET_BOSS_REMAIN 3 @ xnoniAT8
SET_BOSS_HPRANGE 21851 26587
SET_HITPOINT 26587
REGISTER_BOSSBREAK @KTn3jFSl
DROP_POINTITEM 35
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 25
SET_PHASE_BONUS 52 207
WAIT 30

STORE 3 -1
STORE 6 9
STORE 5 %PLAYER_X% @ bpDq1Y7E
SUB 5 10
MOVE_RANDOM %VAR_5% 60 20 80 135 EASE_OUT_CUBIC
CALC_DIRECTION 7 %ENEMY_X% %ENEMY_Y% %PLAYER_X% %PLAYER_Y%
STORE 0 5
MUL 0 %VAR_6%
SUB 7 %VAR_0%
STORE 0 0
    STORE 1 %VAR_0% @ ALoIKfNe
    MOD 1 4
    JUMP_NE %VAR_1% 0 @8FWDyKFU
    SFX ENEMY_SHOOT_D
    SHOOT_MULTIWAY TYPE_C 0 0 2.5 %VAR_7% 240 3 @ 8FWDyKFU
    ADD 7 %VAR_3%
    SHOOT_MULTIWAY TYPE_C 0 0 3 %VAR_7% 240 3
    ADD 7 %VAR_3%
    SHOOT_MULTIWAY TYPE_C 0 0 3.5 %VAR_7% 240 3
    SUB 7 %VAR_3%
    SUB 7 %VAR_3%
    ADD 7 %VAR_6%
    WAIT 3
INC 0
JUMP_L %VAR_0% 50 @ALoIKfNe
MUL 3 -1
MUL 6 -1
JUMP @bpDq1Y7E

SET_BOSS_REMAIN 2 @ KTn3jFSl
SET_BOSS_HPRANGE 14998 21851
SET_HITPOINT 21851
REGISTER_BOSSBREAK @1O1hxUCh
DROP_POINTITEM 35
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 224 90 EASE_OUT_CUBIC
SET_INVINCIBLE 120
WAIT 90
SET_PHASE_NAME 26
SET_PHASE_BONUS 53 213
WAIT 30

STORE 0 0 # 帧数
STORE 1 0 # 角度
STORE 2 0 # 角度的增量（+-45）
STORE 3 .25 # 角度的增量的增加/减少值
# 另外实际上要用二次函数来把%VAR_2%处理成像sin的样子
# k = 30, -k <= x <= k
# sin版：
# k * sin(pi * x / 2 / k)
# 二次版（比较接近sin）：
# x > 0 --->  k + (x - k) ** 2 / -k
# x < 0 ---> -k + (x + k) ** 2 /  k
# 三次版：
# x > 0 --->  k + (x - k) ** 3 / k ** 2
# x < 0 ---> -k + (x + k) ** 3 / k ** 2
# 处理后的值保存在%VAR_4%
    STORE 7 %VAR_0% @ v4zNKEhX
    MOD 7 4
    JUMP_NE %VAR_7% 0 @FQw4uiD8
    SFX ENEMY_SHOOT_D

    SHOOT_MULTIWAY TYPE_D 0 0 2 %VAR_1% 288 5 @ FQw4uiD8

    INC 0
    STORE 4 %VAR_2%
    JUMP_L %VAR_2% 0 @eiZ8au8C
    # x > 0
        SUB 4 30
        MUL 4 %VAR_4%
        FDIV 4 -30
        ADD 4 30
    JUMP @53yhmBc6
    # x < 0
        ADD 4 30 @ eiZ8au8C
        MUL 4 %VAR_4%
        FDIV 4 30
        SUB 4 30

    ADD 1 %VAR_4% @ 53yhmBc6
    ADD 2 %VAR_3%
    JUMP_G %VAR_2% 30 @uV9zFJPm
    JUMP_L %VAR_2% -30 @uV9zFJPm
    JUMP @XcQwQ4Na
    MUL 3 -1 @ uV9zFJPm
    WAIT 3 @ XcQwQ4Na
JUMP @v4zNKEhX

SET_BOSS_REMAIN 1 @ 1O1hxUCh
SET_BOSS_HPRANGE 9999 14998
SET_HITPOINT 14998
REGISTER_BOSSBREAK @YFRp9lly
DROP_POINTITEM 37
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 108 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 27
SET_PHASE_BONUS 54 217
WAIT 30

STORE 6 12
STORE 5 %PLAYER_X% @ TvOtomim
SUB 5 10
MOVE_RANDOM %VAR_5% 60 20 80 135 EASE_OUT_CUBIC
CALC_DIRECTION 7 %ENEMY_X% %ENEMY_Y% %PLAYER_X% %PLAYER_Y%
STORE 0 5
MUL 0 %VAR_6%
SUB 7 %VAR_0%
STORE 0 0
    STORE 1 %VAR_0% @ xFbkKl7N
    MOD 1 2
    JUMP_NE %VAR_1% 0 @Yf3X3X8o
    SFX ENEMY_SHOOT_D
    SHOOT_MULTIWAY TYPE_C 0 0 1.5 %VAR_7% 320 9 @ Yf3X3X8o
    SHOOT_MULTIWAY TYPE_C 0 0 2.25 %VAR_7% 320 9
    ADD 7 %VAR_6%
    WAIT 6
INC 0
JUMP_L %VAR_0% 40 @xFbkKl7N
MUL 6 -1
JUMP @TvOtomim

SET_BOSS_REMAIN 0 @ YFRp9lly
SET_BOSS_HPRANGE 0 9999
SET_HITPOINT 9999
REGISTER_DEATH @496GR0U8
DROP_POINTITEM 39
BONUS_PHASE
BONUS_BULLET
SET_PHASE_NAME 0
MOVE_CLEAR
MOVE 192 54 120 EASE_INOUT_CUBIC
SET_INVINCIBLE 150
WAIT 120
SET_PHASE_NAME 28
SET_PHASE_BONUS 64 269
WAIT 30

STORE 0 0
STORE 6 0
STORE 7 0
    STORE 1 %VAR_0% @ xFDYWmcG
    MOD 1 90
    JUMP_NE %VAR_1% 0 @mAGKL8nj
    # 十二边形仿椭圆移动，一共900f
    MOVE 269 76 75 LINEAR
    MOVE 326 139 75 LINEAR
    MOVE 347 224 75 LINEAR
    MOVE 326 309 75 LINEAR
    MOVE 269 372 75 LINEAR
    MOVE 192 394 75 LINEAR
    MOVE 115 372 75 LINEAR
    MOVE 58 309 75 LINEAR
    MOVE 37 224 75 LINEAR
    MOVE 58 139 75 LINEAR
    MOVE 115 76 75 LINEAR
    MOVE 192 54 75 LINEAR

    STORE 1 %VAR_0% @ mAGKL8nj
    MOD 1 2
    JUMP_NE %VAR_1% 0 @bXRXsQsv
    SFX ENEMY_SHOOT_D
    SHOOT TYPE_C 0 0 1.5 %VAR_6% @ bXRXsQsv
    SHOOT TYPE_C 0 0 2 %VAR_6%
    SHOOT TYPE_C 0 0 2.5 %VAR_6%
    ADD 6 13
    ADD 7 19

    JUMP_G %HITPOINT% 6999 @aiN9WKKl
    STORE 1 %VAR_0%
    MOD 1 6
    JUMP_NE %VAR_1% 0 @aiN9WKKl
    SFX ENEMY_SHOOT_B
    SHOOT_AIMING TYPE_A 8.5 21 2 0
    SHOOT_AIMING TYPE_A 8.5 -21 2 0
    SHOOT_AIMING TYPE_A -8.5 21 2 0
    SHOOT_AIMING TYPE_A -8.5 -21 2 0
    SHOOT_AIMING TYPE_A 21 8.5 2 0
    SHOOT_AIMING TYPE_A 21 -8.5 2 0
    SHOOT_AIMING TYPE_A -21 8.5 2 0
    SHOOT_AIMING TYPE_A -21 -8.5 2 0

    JUMP_G %HITPOINT% 3499 @zw5yPpAD @ aiN9WKKl
    STORE 1 %VAR_0%
    MOD 1 8
    JUMP_NE %VAR_1% 0 @zw5yPpAD
    SFX ENEMY_SHOOT_C
    SHOOT_AIMING_MULTIWAY TYPE_B 0 0 1 0 360 25

    JUMP_G %HITPOINT% 1499 @d9u0P0pZ @ zw5yPpAD
    STORE 1 %VAR_0%
    MOD 1 2
    JUMP_NE %VAR_1% 0 @m7LUGkR4
    SFX ENEMY_SHOOT_E
    SHOOT TYPE_D 0 0 1.5 %VAR_7% @ m7LUGkR4
    SHOOT TYPE_D 0 0 1.7 %VAR_7%
    SHOOT TYPE_D 0 0 1.9 %VAR_7%
    SHOOT TYPE_D 0 0 2.1 %VAR_7%

    WAIT 10 @ d9u0P0pZ
INC 0
JUMP @xFDYWmcG

BONUS_PHASE @ 496GR0U8
BONUS_BULLET
SET_PHASE_NAME 0
DROP_POINTITEM 45
HALT